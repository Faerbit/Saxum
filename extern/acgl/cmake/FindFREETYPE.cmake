# - Find FREETYPE
# Find the native FREETYPE headers
#
#  FREETYPE_INCLUDE_DIR  - where to find FREETYPE.h, etc.
#  FREETYPE_LIBRARIES    - FREETYPE library
#  FREETYPE_DEFINES      - FREETYPE defines
#  FREETYPE_FOUND        - True if FREETYPE found.

IF(FREETYPE_FOUND)
    # Already in cache, be silent
    SET(FREETYPE_FIND_QUIETLY TRUE)
ENDIF()

IF(BUILD_FREETYPE OR ACGL_GLOBAL_EXTERN_DIR_FOUND_FIRST_TIME OR BUILD_TYPE_CHANGED)
    UNSET(FREETYPE_INCLUDE_DIR CACHE)
    UNSET(FREETYPE_LIBRARIES CACHE)
ENDIF()

IF(BUILD_FREETYPE)
    SET(FREETYPE_INCLUDE_DIR "${ACGL_LOCAL_EXTERN_DIR}/freetype/include")
    SET(FREETYPE_LIBRARIES freetype${COMPILE_POSTFIX})
ELSE()
    # Look for the header file.
    IF(ACGL_GLOBAL_EXTERN_DIR)
        FIND_PATH(FREETYPE_INCLUDE_DIR NAMES ft2build.h PATHS "${ACGL_GLOBAL_EXTERN_DIR}/freetype/include" NO_DEFAULT_PATH)
    ENDIF()
    IF(NOT FREETYPE_INCLUDE_DIR)
        FIND_PATH(FREETYPE_INCLUDE_DIR NAMES ft2build.h)
    ENDIF()

    # Look for the library file.
    IF(ACGL_GLOBAL_EXTERN_DIR)
        FIND_LIBRARY(FREETYPE_LIBRARIES NAMES freetype${COMPILE_POSTFIX} PATHS "${ACGL_GLOBAL_EXTERN_DIR}/freetype/lib" NO_DEFAULT_PATH)
    ENDIF()
    IF(NOT FREETYPE_LIBRARIES)
        FIND_LIBRARY(FREETYPE_LIBRARIES NAMES freetype${COMPILE_POSTFIX})
    ENDIF()
ENDIF()

SET(FREETYPE_CONFIG_PATH ${FREETYPE_INCLUDE_DIR}/freetype/config)
INCLUDE(${CMAKE_CURRENT_LIST_DIR}/DefinesFREETYPE.txt)

# Copy the results to the output variables.
IF(FREETYPE_INCLUDE_DIR AND FREETYPE_LIBRARIES)
    SET(FREETYPE_FOUND TRUE CACHE INTERNAL "")
ELSE()
    SET(FREETYPE_FOUND FALSE CACHE INTERNAL "")
ENDIF()

# Report the results.
IF(NOT FREETYPE_FOUND)
    SET(FREETYPE_MESSAGE "FREETYPE was not found. Make sure FREETYPE_INCLUDE_DIR AND FREETYPE_LIBRARIES are set correctly.")
    IF(FREETYPE_FIND_REQUIRED)
        MESSAGE(FATAL_ERROR "${FREETYPE_MESSAGE}")
    ELSEIF(NOT FREETYPE_FIND_QUIETLY)
        MESSAGE(STATUS "${FREETYPE_MESSAGE}")
    ENDIF()
ELSEIF(NOT FREETYPE_FIND_QUIETLY)
    MESSAGE(STATUS "Looking for FREETYPE - found")
ENDIF()

MESSAGE(STATUS "FREETYPE_INCLUDE_DIR:${FREETYPE_INCLUDE_DIR}")
MESSAGE(STATUS "FREETYPE_LIBRARIES:${FREETYPE_LIBRARIES}")

